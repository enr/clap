
// --- release plugin
// see: http://tellurianring.com/wiki/gradle/release
apply from: "https://launchpad.net/gradle-release/trunk/latest/+download/apply.groovy"

apply from:'http://gradle.smokejumperit.com/all-jars.gradle'

buildscript {
    repositories { mavenRepo url: 'http://evgenyg.artifactoryonline.com/evgenyg/repo/' }
    dependencies { classpath "com.github.goldin.plugins:gradle:0.1.3" }
}

ext {
    groovyVersion = '2.0.5'
    javaModules = [
        ':modules:core',
        ':modules:uat'
    ]
    uploadableModules = [
        ':modules:core'
    ]
    javaVersion = System.getProperty('java.version')
    java7 = javaVersion.startsWith('1.7')
}

allprojects { project ->
    group = 'com.github.enr'
    
    repositories {
        mavenLocal()
        mavenCentral()    
        maven {
            url 'https://oss.sonatype.org/content/repositories/releases/'
        }
    }

}

subprojects {

    if (project.path in javaModules) {
        apply plugin: 'groovy'
        apply plugin: 'eclipse'
        apply plugin: 'maven'

        configurations {
            compile.transitive = true
            testCompile.transitive = true
        }

        dependencies {
            groovy "org.codehaus.groovy:groovy-all:$groovyVersion"
            compile "org.codehaus.groovy:groovy-all:$groovyVersion",
                    'com.google.inject:guice:3.0'
        }

        //archivesBaseName = "clap-${project.name}"

        sourceCompatibility = 1.6
        targetCompatibility = 1.6

        jar {
            manifest {
                attributes( "Implementation-Title": "Clap",
                            "Implementation-Version": version,
                            "Project":project.name)
            }
        }
        
        eclipse {
            project {
                name = "clap-${project.name}"
            }
            classpath {
                defaultOutputDir = new File("${project.buildDir}/eclipse/classes");
                downloadJavadoc = true
                downloadSources = true
            }
        }
    }
    
    if (project.path in uploadableModules) {
        
        //apply from:'/home/enrico/Projects/incub-gradle/bintray-maven.gradle'
        
        ext.pomPath = "${buildDir}/${project.name}-${project.version}.pom"
        
        task writeNewPom << {
            pom {
                project {
                    inceptionYear '2013'
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                }
            }.writeTo(pomPath)
        }

        createReleaseTag.dependsOn uploadArchives
        
        task sourceJar(type: Jar) {
            from sourceSets.main.allSource
            classifier = 'sources'
        }

        task javadocJar(type: Jar, dependsOn: 'javadoc') {
            from javadoc.destinationDir
            classifier = 'javadoc'
        }

        artifacts {
            archives sourceJar, javadocJar, file(pomPath)
        }

        uploadArchives.dependsOn writeNewPom
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.2'
}
